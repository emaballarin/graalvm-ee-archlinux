# Maintainer: Lucas Werkmeister <mail@lucaswerkmeister.de>

pkgname_=fastr
pkgname=${pkgname_}-bin
pkgver_=19.2.0
pkgver=${pkgver_/-/_}
pkgrel=1
pkgdesc='Graal based, high-performance implementation of the R language'
arch=('x86_64')
url='https://github.com/oracle/fastr'
license=('GPL3')
depends=('graal')
makedepends=()
optdepends=()
provides=("$pkgname_")
conflicts=("$pkgname_")
source=("https://github.com/oracle/$pkgname_/releases/download/vm-${pkgver_}/r-installable-linux-amd64-${pkgver_}.jar")
sha256sums=('SKIP')
PKGEXT='.pkg.tar' # Do not compress

package() {
    # A band-aid fix to prevent an error
    mkdir -p "$pkgdir/usr/lib/jvm/java-8-graal/"
    cp -f "$srcdir/LICENSE_FASTR" "$pkgdir/usr/lib/jvm/java-8-graal/./LICENSE_FASTR"
    cp -f "$srcdir/3rd_party_licenses_fastr.txt" "$pkgdir/usr/lib/jvm/java-8-graal/./3rd_party_licenses_fastr.txt"

    # Go ahead as usual...
    local file eq permissions mode name target

    mkdir -p "$pkgdir/usr/lib/jvm/java-8-graal/"
    cp -a -t "$pkgdir/usr/lib/jvm/java-8-graal/" jre/

    printf '\n' >> META-INF/permissions
    while read -r file eq permissions; do
        if [[ $eq != '=' ]]; then
            printf >&2 'second word should be "=": %s %s %s\n' "$file" "$eq" "$permissions"
            return 1
        fi
        case $permissions in
            'rw-------') mode=600;;
            'rw-r--r--') mode=644;;
            'rw-rw-r--') mode=664;;
            'rwxr-xr-x') mode=755;;
            'rwxrwxr-x') mode=775;;
            'rwxrwxrwx') continue;; # symbolic link
            *)
                printf >&2 'unknown permissions: %s\n' "$permissions"
                return 1
                ;;
        esac
        chmod "$mode" -- "$pkgdir/usr/lib/jvm/java-8-graal/$file"
    done < META-INF/permissions

    printf '\n' >> META-INF/symlinks
    while read -r name eq target; do
        if [[ $eq != '=' ]]; then
            printf >&2 'second word should be "=": %s %s %s\n' "$name" "$eq" "$target"
            return 1
        fi
        mkdir -p -- "$pkgdir/usr/lib/jvm/java-8-graal/$(dirname -- "$name")"
        ln -s -- "$target" "$pkgdir/usr/lib/jvm/java-8-graal/$name"
    done < META-INF/symlinks

    install -DTm644 jre/languages/R/LICENSE_FASTR "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    mkdir -p "$pkgdir/usr/bin/"
    ln -s ../lib/jvm/java-8-graal/bin/R "$pkgdir/usr/bin/graal-R"
    ln -s ../lib/jvm/java-8-graal/bin/R "$pkgdir/usr/bin/graalR"
    ln -s ../lib/jvm/java-8-graal/bin/R "$pkgdir/usr/bin/fastr"
    ln -s ../lib/jvm/java-8-graal/bin/R "$pkgdir/usr/bin/fastR"
    ln -s ../lib/jvm/java-8-graal/bin/R "$pkgdir/usr/bin/graal-fastr"
    ln -s ../lib/jvm/java-8-graal/bin/R "$pkgdir/usr/bin/graal-fastR"
    ln -s ../lib/jvm/java-8-graal/bin/Rscript "$pkgdir/usr/bin/graal-Rscript"
    ln -s ../lib/jvm/java-8-graal/bin/Rscript "$pkgdir/usr/bin/graalRscript"
    ln -s ../lib/jvm/java-8-graal/bin/Rscript "$pkgdir/usr/bin/graalrscript"
}
